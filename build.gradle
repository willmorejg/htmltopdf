plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.11'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'com.diffplug.spotless' version '6.15.0'
	id 'org.ysb33r.doxygen' version '0.7.0'
	id 'jacoco'
	id 'project-report'
	id 'build-dashboard'
}

group = 'net.ljcomputing'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()

	maven {
		url = uri('http://localhost:8081/repository/maven-central')
		allowInsecureProtocol = true
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-freemarker'

	implementation 'com.fasterxml.jackson.core:jackson-core:2.15.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.15.0'

	implementation 'com.openhtmltopdf:openhtmltopdf-core:1.0.10'
	implementation 'com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10'
	implementation 'com.openhtmltopdf:openhtmltopdf-svg-support:1.0.10'
	implementation 'com.openhtmltopdf:openhtmltopdf-jsoup-dom-converter:1.0.0'

	implementation 'org.apache.pdfbox:pdfbox:2.0.24'

	implementation 'com.github.jtidy:jtidy:1.0.3'

	implementation 'org.bouncycastle:bcjmail-jdk18on:1.73'
	implementation 'org.bouncycastle:bcpkix-jdk18on:1.73'
	implementation 'org.bouncycastle:bcprov-jdk18on:1.73'
	implementation 'org.bouncycastle:bcutil-jdk18on:1.73'
	implementation 'org.bouncycastle:bcpg-jdk18on:1.73'

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
	withJavadocJar()
	withSourcesJar()
}

tasks.withType(JavaCompile) {
	dependsOn spotlessApply
	options.encoding = 'UTF-8'
}

bootRun {
	classpath = sourceSets.main.runtimeClasspath
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}

	manifest {
		attributes (
				'Main-Class': 'net.ljcomputing.htmltopdf.HtmltopdfApplication',
				'Implementation-Title': 'HTML to PDF Spike',
				'Implementation-Version': version
				)
	}
}

spotless {
	java {
		targetExclude("build/**")
		licenseHeaderFile("$rootProject.projectDir/configuration/spotless/license-header.java").updateYearWithLatest(true)
		toggleOffOn()
		trimTrailingWhitespace()
		endWithNewline()
		removeUnusedImports()
		importOrder('java', 'javax', '', '\\#')
		googleJavaFormat('1.15.0').aosp().reflowLongStrings().groupArtifact('com.google.googlejavaformat:google-java-format')
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}

javadoc {
	exclude("build/generated/sources/**/*.java")
	options.memberLevel = JavadocMemberLevel.PRIVATE
	failOnError(false)
}

doxygen {
	source 'src/main/java'
	project_name 'HTML to PDF Spike'
	generate_latex true
	generate_html true
	optimize_output_java true
}

test {
	testLogging.showStandardStreams = true
	finalizedBy jacocoTestReport
	useJUnitPlatform()
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 1.0
			}
		}
	}
}
